plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
    minecraft = rootProject.minecraft_version
}

forgix {
    group = "${rootProject.maven_group}.${rootProject.archives_name}"
    mergedJarName = "${rootProject.archives_name}-${rootProject.mod_version}-${rootProject.minecraft_version}.jar"
    outputDir = "build/libs/merged"

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/${rootProject.archives_name}-fabric-${rootProject.mod_version}.jar"
    }

    forge {
        projectName = "forge"
        jarLocation = "build/libs/${rootProject.archives_name}-forge-${rootProject.mod_version}.jar"
    }

    neoforge {
        projectName = "neoforge"
        jarLocation = "build/libs/${rootProject.archives_name}-neoforge-${rootProject.mod_version}.jar"
    }
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = "${rootProject.archives_name}-${project.name}"
    }

    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)

    repositories {
        maven {
            name = "ParchmentMC"
            url = "https://maven.parchmentmc.org"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment "org.parchmentmc.data:parchment-${rootProject.parchment_version}@zip"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = rootProject.maven_group
                artifactId = project.base.archivesName
                version = "${rootProject.mod_version}-${rootProject.minecraft_version}"
                from components.java
            }
        }

        repositories {
        }
    }
}